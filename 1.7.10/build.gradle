buildscript {
	repositories {
		mavenCentral()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven/"
		}
		maven {
			name "sonatype"
			url "https://oss.sonatype.org/content/repositories/snapshots"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
	}
}

apply plugin: "forge"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = mc_version + "-" + mod_version

archivesBaseName = "DiscordChat"

minecraft {
	version = mc_version + "-" + forge_version
	runDir = "run"

	mappings = mcp_mappings

	replaceIn "OneSevenTenMod.java"
	replace "@VERSION@", mod_version

	srgExtra "PK: com/iwebpp/crypto net/shadowfacts/discordchat/repack/com/iwebpp/crypto"
	srgExtra "PK: com/neovisionaries/ws/client net/shadowfacts/discordchat/repack/com/neovisionaries/ws/client"
	srgExtra "PK: com/sun/jna net/shadowfacts/discordchat/repack/com/sun/jna"
	srgExtra "PK: com/vdurmont/emoji net/shadowfacts/discordchat/repack/com/vdurmont/emoji"
	srgExtra "PK: gnu/trove net/shadowfacts/discordchat/repack/gnu/trove"
	srgExtra "PK: net/dv8tion/jda net/shadowfacts/discordchat/repack/net/dv8tion/jda"
	srgExtra "PK: okhttp3 net/shadowfacts/discordchat/repack/okhttp3"
	srgExtra "PK: okio net/shadowfacts/discordchat/repack/okio"
	srgExtra "PK: org/apache/commons/collections4 net/shadowfacts/discordchat/repack/org/apache/commons/collections4"
	srgExtra "PK: org/apache/commons/lang3 net/shadowfacts/discordchat/repack/org/apache/commons/lang3"
	srgExtra "PK: org/json net/shadowfacts/discordchat/repack/org/json"
	srgExtra "PK: tomp2p/opuswrapper net/shadowfacts/discordchat/repack/tomp2p/opuswrapper"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

configurations {
	shade
	compile.extendsFrom shade
}

repositories {
	maven {
		name "shadowfacts"
		url "http://maven.shadowfacts.net/"
	}
	flatDir {
		dirs "../core/libs"
	}
}

dependencies {
	shade project(":core")
}

jar {
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude "META-INF", "META-INF/**"
		}
	}
}