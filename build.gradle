buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
	}
}

plugins {
	id "com.matthewprenger.cursegradle" version "1.0.6"
	id "com.github.johnrengelman.shadow" version "1.2.2"
}

apply plugin: "net.minecraftforge.gradle.forge"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = mc_version + "-" + mod_version

minecraft {
	version = mc_version + "-" + forge_version
	runDir = "run"

	mappings = mcp_mappings
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

repositories {
	maven {
		name "shadowfacts"
		url "http://mvn.rx14.co.uk/shadowfacts"
	}
	flatDir {
		dirs "libs"
	}

	jcenter()
}

dependencies {
	compile group: "net.shadowfacts", name: "ShadowMC", version: mc_version + "-" + shadowmc_version

	shade 'net.dv8tion:JDA:2.0.0_242'
}

shadowJar {
	classifier = ""
	relocate "com.mashape.unirest", "net.shadowfacts.discordchat.repack.com.mashape.unirest"
	relocate "com.neovisionaries.ws.client", "net.shadowfacts.discordchat.repack.com.neovisionaries.ws.client"
	relocate "com.sun.jna", "net.shadowfacts.discordchat.repack.com.sun.jna"
	relocate "javax.sound", "net.shadowfacts.discordchat.repack.javax.sound"
	relocate "javazoom", "net.shadowfacts.discordchat.repack.javazoom"
	relocate "net.dv8tion.jda", "net.shadowfacts.discordchat.repack.net.dv8tion.jda"
	relocate "net.sourceforge.jaad", "net.shadowfacts.discordchat.repack.net.sourceforge.jaad"
	relocate "org.apache.commons.codec", "net.shadowfacts.discordchat.repack.org.apache.commons.codec"
	relocate "org.apache.commons.lang3", "net.shadowfacts.discordchat.repack.org.apache.commons.lang3"
	relocate "org.json", "net.shadowfacts.discordchat.repack.org.json"
	relocate "org.kc7bfi.jflac", "net.shadowfacts.discordchat.repack.orgkc7bfi.jflac"
	relocate "org.tritonus", "net.shadowfacts.discordchat.repack.org.tritonus"
	relocate "tomp2p.opuswrapper", "net.shadowfacts.discordchat.repack.tomp2p.opuswrapper"
	
	relocate "org.apache.http", "net.shadowfacts.discordchat.repack.org.apache.http"
	relocate "org.apache.commons.logging", "net.shadowfacts.discordchat.repack.org.apache.commons.logging"

	dependencies {
		include(dependency("net.dv8tion:JDA:1.2.2_167:withDependencies"))
	}
}

curseforge {
	apiKey = project.hasProperty("curseForgeApiKey") ? project.curseForgeApiKey : ""

	project {
		id = "240995"

		addGameVersion "1.8.8"
		addGameVersion "1.8.9"
		addGameVersion "Java 8"

		mainArtifact(shadowJar) {
			releaseType = "release"
			changelog = ""

			relations {
				requiredLibrary "shadowmc"
			}
		}
	}
}
reobf { shadowJar { mappingType = "SEARGE" } }
tasks.reobfShadowJar.mustRunAfter shadowJar
afterEvaluate {
	tasks.curseforge240995.dependsOn reobfShadowJar
}
